apply plugin: 'java'
apply plugin: 'checkstyle'
apply plugin: 'findbugs'
apply plugin: 'pmd'


sourceCompatibility = 7

repositories {
    mavenCentral()
}

checkstyle {
    toolVersion = "6.7"
}

findbugs {
  ignoreFailures = true
  sourceSets = [sourceSets.main]
  effort = "max"
}

pmd {
  ignoreFailures = true
  sourceSets = [sourceSets.main]
}

test {
  reports.junitXml.destination = file('build/reports/tests')
}

sourceSets {
  main {
        java {
            srcDir 'cases/src'
            srcDir 'backend/src'
            srcDir 'api'
        }
  }
  test {
      java {
          srcDir 'tests/test'
      }
      resources{
          srcDir 'tests/resources'
      }
  }

  cli {
      java {
          srcDir 'util/src'
      }
  }
}

dependencies {
    compile project(':javarosa')
    testCompile 'junit:junit:4.+'
    compile 'org.xerial:sqlite-jdbc:3.8.10.1'
    compile 'org.json:json:20090211'
    cliCompile project(path: ':javarosa', configuration: 'harnessOutput')
    cliCompile sourceSets.main.output
    testCompile sourceSets.main.output
    testCompile sourceSets.cli.output
    testCompile group: 'junit', name: 'junit', version: '4.+'
}

jar {
    baseName = "commcare-libraries"
}

jar.dependsOn test

configurations {
  cliOutput.extendsFrom (cliCompile)
}

task cliJar(type: Jar, dependsOn: cliClasses) {
    baseName = "commcare-cli"
    from sourceSets.cli.output
}

artifacts {
  cliOutput cliJar
}

task copyTestResources(type: Copy) {
    from sourceSets.test.resources
    into "${buildDir}/classes/test"
}
processTestResources.dependsOn copyTestResources

task jenkinsTest {
    inputs.files test.outputs.files
    doLast {
        def timestamp = System.currentTimeMillis()
        test.getReports().getJunitXml().getDestination().eachFile { it.lastModified = timestamp }
    }
}

check.dependsOn(jenkinsTest)
