apply plugin: 'java'

// Needed even though it looks unused
sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        mavenCentral()
    }
    dependencies {
        classpath "me.champeau.gradle:jmh-gradle-plugin:0.5.3"
    }
}

apply plugin: 'me.champeau.gradle.jmh'

test {
  reports.junitXml.destination = file('build/reports/tests')
}

sourceSets {
    translate {
        java {
            compileClasspath += main.output
        }
    }

  cli {
      java {
          compileClasspath += main.output
          compileClasspath += translate.output
      }
  }
    api {
        java {
            compileClasspath += main.output
            srcDir 'src/cli/java'
            exclude 'org/commcare/util/cli/**'
            exclude 'org/commcare/util/mocks/**'
        }
    }

    test {
        java {
            compileClasspath += cli.output
            compileClasspath += api.output
            compileClasspath += translate.output
        }
    }


}

dependencies {
    implementation 'com.github.stefanhaustein:kxml2:2.4.1'
    implementation 'javax.ws.rs:javax.ws.rs-api:2.0.1'
    implementation group: 'regexp', name: 'regexp', version: '1.3'
    // Added so Android Studio recognizes libs in util jar projects
    implementation 'org.json:json:20140107'
    implementation 'commons-cli:commons-cli:1.3.1'
    implementation 'xpp3:xpp3:1.1.4c'
    implementation 'joda-time:joda-time:2.9.4'
    implementation 'com.carrotsearch:hppc:0.7.2'
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.okhttp3:okhttp:3.12.12'
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.1'

    cliImplementation 'org.json:json:20140107'
    cliImplementation group: 'regexp', name: 'regexp', version: '1.3'
    cliImplementation 'com.github.stefanhaustein:kxml2:2.4.1'
    cliImplementation 'commons-cli:commons-cli:1.3.1'
    cliImplementation 'xpp3:xpp3:1.1.4c'
    cliImplementation 'joda-time:joda-time:2.9.4'
    cliImplementation 'io.reactivex.rxjava2:rxjava:2.1.1'
    cliImplementation 'com.squareup.retrofit2:retrofit:2.3.0'
    cliImplementation 'com.squareup.okhttp3:okhttp:4.4.1'

    testImplementation sourceSets.main.output
    testImplementation sourceSets.api.output
    testImplementation sourceSets.cli.output
    testImplementation sourceSets.translate.output
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.json:json:20140107'
    testImplementation 'org.xerial:sqlite-jdbc:3.28.0'
    testImplementation 'joda-time:joda-time:2.9.4'
    testImplementation 'com.squareup.retrofit2:retrofit:2.3.0'
    testImplementation 'com.squareup.okhttp3:okhttp:4.4.1'

    apiImplementation sourceSets.main.output
    apiImplementation 'org.json:json:20140107'
    apiImplementation 'org.xerial:sqlite-jdbc:3.28.0'
    apiImplementation 'com.github.stefanhaustein:kxml2:2.4.1'
    apiImplementation group: 'regexp', name: 'regexp', version: '1.3'
    apiImplementation 'joda-time:joda-time:2.9.4'
    apiImplementation 'com.carrotsearch:hppc:0.7.2'
    apiImplementation 'io.reactivex.rxjava2:rxjava:2.1.1'
    apiImplementation "com.squareup.retrofit2:retrofit:2.3.0"
    apiImplementation 'com.squareup.okhttp3:okhttp:4.4.1'

    translateImplementation 'com.github.stefanhaustein:kxml2:2.4.1'
    translateImplementation 'xpp3:xpp3:1.1.4c'
    translateImplementation 'commons-cli:commons-cli:1.3.1'
    translateImplementation group: 'regexp', name: 'regexp', version: '1.3'
    translateImplementation files('lib/json-simple-1.1.1.jar')
    translateImplementation 'joda-time:joda-time:2.9.4'
    translateImplementation 'org.json:json:20140107'
    translateImplementation 'io.reactivex.rxjava2:rxjava:2.1.1'
    translateImplementation "com.squareup.retrofit2:retrofit:2.3.0"
    translateImplementation 'com.squareup.okhttp3:okhttp:4.4.1'

    // 'compile' dependency is also needed to run 'gradle jmh' for some reason,
    // but leaving it in conflicts with apk builds, so only uncomment the
    // following lines when jmh needed:
    /*
            compile 'org.openjdk.jmh:jmh-core:1.17.3'
            compile 'org.openjdk.jmh:jmh-generator-annprocess:1.17.3'
            jmhCompile 'org.openjdk.jmh:jmh-core:1.17.3'
            jmhCompile 'org.openjdk.jmh:jmh-generator-annprocess:1.17.3'
            */
}

jar {
    baseName = "commcare-libraries"
}

jar.dependsOn test

configurations {
  api
  cliOutput.extendsFrom (cliCompile)
  harness
  translate
  testsAsJar
}

task cliJar(type: Jar, dependsOn: cliClasses) {
    baseName = "commcare-cli"
    from sourceSets.translate.output
    from sourceSets.cli.output
    from sourceSets.main.output
    from {
        configurations.cliCompile.collect {
            it.isDirectory() ? it : zipTree(it)
        }
        configurations.cliRuntime.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    manifest {
        attributes 'Main-Class': 'org.commcare.util.cli.CliMain'
    }

}

task apiJar(type: Jar, dependsOn: apiClasses) {
    baseName = "commcare-api"
    from sourceSets.api.output
    from sourceSets.main.output
    from {
        configurations.apiCompile.collect {
            it.isDirectory() ? it : zipTree(it)
        }
        configurations.apiRuntime.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

task harnessJar(type: Jar, dependsOn: translateClasses) {
    baseName = "javarosa-cli"
    from sourceSets.translate.output
    from sourceSets.main.output
    from {
        configurations.compile.collect {
            it.isDirectory() ? it : zipTree(it)
        }
        configurations.runtime.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    manifest {
        attributes 'Main-Class': 'org.javarosa.engine.Harness'
    }
}

// used to provide the test source files to external projects, such as commcare-android,
// which might want to import classes
task testsrcJar(type: Jar, dependsOn: testClasses) {
    classifier = 'tests'
    from files(sourceSets.test.output)
    from files(sourceSets.cli.output)
}


task formTranslateJar(type: Jar, dependsOn: translateClasses) {
    baseName = "form_translate"
    from sourceSets.translate.output
    from sourceSets.main.output
    from {
        configurations.translateCompile.collect {
            it.isDirectory() ? it : zipTree(it)
        }
        configurations.translateRuntime.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    manifest {
        attributes 'Main-Class': 'org.javarosa.xform.schema.Harness'
    }
}

artifacts {
  cliOutput cliJar
  api apiJar
  harness harnessJar
  translate formTranslateJar
  testsAsJar testsrcJar
}

task copyTestResources(type: Copy) {
    from sourceSets.test.resources
    into "${buildDir}/classes/test"
}
processTestResources.dependsOn copyTestResources

def buildDeps = hasProperty('javarosaDeps') ? javarosaDeps : null

/* for use primarily by the build server to extract the required 3rd-party
libraries into the javarosa source tree. to use this in your own build, set the
'extract-libs-fresh' property, then also set the 'javarosa-deps' property to
the path of the library archive (make sure you are using the version of the
archive compatible with the version you wish to build) */
task extractLibs(type: Copy) {
    if (buildDeps != null) {
        from zipTree(file("${buildDeps}")) into file("${projectDir}")
    }
}

compileJava.dependsOn extractLibs


task jenkinsTest {
    inputs.files test.outputs.files
    doLast {
        def timestamp = System.currentTimeMillis()
        test.getReports().getJunitXml().getDestination().eachFile { it.lastModified = timestamp }
    }
}

check.dependsOn(jenkinsTest)

jmh {
    duplicateClassesStrategy = 'warn'
    fork = 2
    iterations = 10
    warmupIterations = 20
    includeTests = true
    // default mode is 'throughput'; where a larger score is better
}
